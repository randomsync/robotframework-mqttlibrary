| *Settings*    | *Value*
| Resource      | keywords.txt
| Test Timeout  | 30 seconds

| *Test Cases*
| Publish a non-empty message
| | ${time}     | Get Time      | epoch
| | ${message}  | Catenate      | test message  | ${time}
| | Publish to MQTT Broker and Disconnect       | message=${message}

| Publish an empty message
| | Publish to MQTT Broker and Disconnect

| Publish a message with QOS 1 and validate that the message is received
| | ${time}     | Get Time      | epoch
| | ${client}   | Catenate      | SEPARATOR=.   | robot.mqtt | ${time}
| | ${topic}    | Set Variable  | test/mqtt_test_sub
| | ${message}  | Set Variable  | subscription test message
| |  Run Keyword And Expect Error               | The expected payload didn't arrive in the topic
| | ... | Subscribe to MQTT Broker and Validate | client.id=${client}   | topic=${topic}        | message=${message}
| | Publish to MQTT Broker and Disconnect       | topic=${topic}        | message=${message}    | qos=1
| | Subscribe to MQTT Broker and Validate       | client.id=${client}   | topic=${topic}        | message=${message}

| Publish multiple messages and confirm that validation succeeds only after correct message is published
| | Sleep       | 1s
| | ${time}     | Get Time      | epoch
| | ${client}   | Catenate      | SEPARATOR=.   | robot.mqtt | ${time}
| | ${topic}    | Set Variable  | test/mqtt_test_sub
| | ${message}  | Set Variable  | subscription test message
| |  Run Keyword And Expect Error               | The expected payload didn't arrive in the topic
| | ... | Subscribe to MQTT Broker and Validate | client.id=${client}   | topic=${topic}        | message=${message}
| | Publish to MQTT Broker and Disconnect       | topic=${topic}        | message=message1      | qos=1
| | Publish to MQTT Broker and Disconnect       | topic=${topic}        | message=message2      | qos=1
| |  Run Keyword And Expect Error               | The expected payload didn't arrive in the topic
| | ... | Subscribe to MQTT Broker and Validate | client.id=${client}   | topic=${topic}        | message=${message}
| | Publish to MQTT Broker and Disconnect       | topic=${topic}        | message=${message}    | qos=1
| | Publish to MQTT Broker and Disconnect       | topic=${topic}        | message=message3      | qos=1
| | Publish to MQTT Broker and Disconnect       | topic=${topic}        | message=message4      | qos=1
| | Subscribe to MQTT Broker and Validate       | client.id=${client}   | topic=${topic}        | message=${message}

| Publish an empty message with QOS 1 and validate
| | Sleep       | 1s
| | ${time}     | Get Time      | epoch
| | ${client}   | Catenate      | SEPARATOR=.   | robot.mqtt | ${time}
| | ${topic}    | Set Variable  | test/mqtt_test_sub
| |  Run Keyword And Expect Error               | The expected payload didn't arrive in the topic
| | ... | Subscribe to MQTT Broker and Validate | client.id=${client}     | topic=${topic}
| | Publish to MQTT Broker and Disconnect       | topic=${topic}          | qos=1
| | Subscribe to MQTT Broker and Validate       | client.id=${client}     | topic=${topic}

| Publish and validate with regular expression
| | Sleep       | 1s
| | ${time}     | Get Time      | epoch
| | ${client}   | Catenate      | SEPARATOR=.   | robot.mqtt | ${time}
| | ${topic}    | Set Variable  | test/mqtt_test_sub
| | ${message}  | Set Variable  | subscription test message
| | ${regex}    | Set Variable  | ^subscription [test]{4} message$
| |  Run Keyword And Expect Error               | The expected payload didn't arrive in the topic
| | ... | Subscribe to MQTT Broker and Validate | client.id=${client}   | topic=${topic}        | message=whatever
| | Publish to MQTT Broker and Disconnect       | topic=${topic}        | message=${message}    | qos=1
| | Subscribe to MQTT Broker and Validate       | client.id=${client}   | topic=${topic}        | message=${regex}

| Subscribe for the first time and validate that no messages are received
| | Sleep       | 1s
| | ${time}     | Get Time      | epoch
| | ${client}   | Catenate      | SEPARATOR=.   | robot.mqtt | ${time}
| | ${topic}    | Set Variable  | test/mqtt_test_sub
| | @{messages} | Subscribe and Get Messages    | client.id=${client}   | topic=${topic}    | timeout=5s
| | LOG         | ${messages}
| | Length Should Be    | ${messages}   | 0

| Subscribe, publish 1 message and validate it is received
| | Sleep       | 1s
| | ${time}     | Get Time      | epoch
| | ${client}   | Catenate      | SEPARATOR=.   | robot.mqtt | ${time}
| | ${topic}    | Set Variable  | test/mqtt_test_sub
| | Subscribe and Get Messages  | client.id=${client}   | topic=${topic}
| | Publish to MQTT Broker and Disconnect   | topic=${topic}    | message=test message      | qos=1
| | @{messages} | Subscribe and Get Messages    | client.id=${client}   | topic=${topic}
| | LOG         | ${messages}
| | Length Should Be            | ${messages}       | 1
| | Should Be Equal As Strings  | @{messages}[0]    | test message

| Subscribe with no limit, publish multiple messages and validate they are received
| | Sleep       | 1s
| | ${time}     | Get Time      | epoch
| | ${client}   | Catenate      | SEPARATOR=.   | robot.mqtt | ${time}
| | ${topic}    | Set Variable  | test/mqtt_test_sub
| | Subscribe and Get Messages  | client.id=${client}   | topic=${topic}
| | Publish to MQTT Broker and Disconnect   | topic=${topic}    | message=test message1 | qos=1
| | Publish to MQTT Broker and Disconnect   | topic=${topic}    | message=test message2 | qos=1
| | Publish to MQTT Broker and Disconnect   | topic=${topic}    | message=test message3 | qos=1
| | @{messages} | Subscribe and Get Messages    | client.id=${client}   | topic=${topic} | limit=0
| | LOG         | ${messages}
| | Length Should Be            | ${messages}       | 3
| | Should Be Equal As Strings  | @{messages}[0]    | test message1
| | Should Be Equal As Strings  | @{messages}[1]    | test message2
| | Should Be Equal As Strings  | @{messages}[2]    | test message3

| Subscribe with limit
| | Sleep       | 1s
| | ${time}     | Get Time      | epoch
| | ${client}   | Catenate      | SEPARATOR=.   | robot.mqtt | ${time}
| | ${topic}    | Set Variable  | test/mqtt_test_sub
| | Subscribe and Get Messages  | client.id=${client}   | topic=${topic}
| | Publish to MQTT Broker and Disconnect   | topic=${topic}    | message=test message1 | qos=1
| | Publish to MQTT Broker and Disconnect   | topic=${topic}    | message=test message2 | qos=1
| | Publish to MQTT Broker and Disconnect   | topic=${topic}    | message=test message3 | qos=1
| | @{messages} | Subscribe and Get Messages    | client.id=${client}   | topic=${topic} | limit=1
| | LOG         | ${messages}
| | Length Should Be            | ${messages}       | 1
| | Should Be Equal As Strings  | @{messages}[0]    | test message1
| | @{messages} | Subscribe and Get Messages    | client.id=${client}   | topic=${topic} | limit=2
| | LOG         | ${messages}
| | Length Should Be            | ${messages}       | 2
| | Should Be Equal As Strings  | @{messages}[0]    | test message2
| | Should Be Equal As Strings  | @{messages}[1]    | test message3

| Unsubscribe and validate no messages are received
| | Sleep       | 1s
| | ${time}     | Get Time      | epoch
| | ${client}   | Catenate      | SEPARATOR=.   | robot.mqtt | ${time}
| | ${topic}    | Set Variable  | test/mqtt_test_sub
| | Subscribe and Get Messages  | client.id=${client}   | topic=${topic}
| | Publish to MQTT Broker and Disconnect   | topic=${topic}    | message=test message1 | qos=1
| | @{messages} | Subscribe and Unsubscribe | client.id=${client}   | topic=${topic}
| | Publish to MQTT Broker and Disconnect   | topic=${topic}    | message=test message2 | qos=1
| | @{messages} | Subscribe and Get Messages    | client.id=${client}   | topic=${topic}
| | LOG         | ${messages}
| | Length Should Be            | ${messages}       | 0

| Publish to a broker that requires username, password authentication
| | [Tags]      | local-only
| | ${time}     | Get Time      | epoch
| | ${client}   | Catenate      | SEPARATOR=.   | robot.mqtt | ${time}
| | ${topic}    | Set Variable  | test
| | Set username and password   | user1         | test1
| | Connect     | 127.0.0.1     | 1883          | ${client}
| | Publish     | ${topic}      | test message with username and password
| | [Teardown]  | Disconnect

| Publish to a broker that requires username, password authentication with invalid password
| | [Tags]      | local-only
| | ${time}     | Get Time      | epoch
| | ${client}   | Catenate      | SEPARATOR=.   | robot.mqtt | ${time}
| | ${topic}    | Set Variable  | test
| | Set username and password   | user1         | test
| | Run Keyword and expect error    | The client disconnected unexpectedly
| | ...         | Connect     | 127.0.0.1     | 1883          | ${client}
| | [Teardown]  | Disconnect

| Subscribe async, publish 1 message, listen for and validate it is received
| | Sleep       | 1s
| | ${time}     | Get Time      | epoch
| | ${client}   | Catenate      | SEPARATOR=.   | robot.mqtt | ${time}
| | ${topic}    | Set Variable  | test/mqtt_test_sub
| | Subscribe Async | client.id=${client}   | topic=${topic}
| | Publish to MQTT Broker   | topic=${topic}    | message=test message      | qos=1
| | Sleep       | 5s
| | @{messages} | Listen and Get Messages    | topic=${topic}
| | LOG         | ${messages}
| | Length Should Be            | ${messages}       | 1
| | Should Be Equal As Strings  | @{messages}[0]    | test message
| | [Teardown]  | Unsubscribe and Disconnect  | ${topic}

| Subscribe async, publish several messages, listen for and validate they are received
| | Sleep       | 1s
| | ${time}     | Get Time      | epoch
| | ${client}   | Catenate      | SEPARATOR=.   | robot.mqtt | ${time}
| | ${topic}    | Set Variable  | test/mqtt_test_sub
| | Subscribe Async | client.id=${client}   | topic=${topic}
| | Publish to MQTT Broker   | topic=${topic}    | message=test message1      | qos=1
| | Sleep       | 1s
| | Publish to MQTT Broker   | topic=${topic}    | message=test message2      | qos=1
| | Sleep       | 1s
| | Publish to MQTT Broker   | topic=${topic}    | message=test message3      | qos=1
| | Sleep       | 5s
| | @{messages} | Listen and Get Messages    | topic=${topic} | limit=0
| | LOG         | ${messages}
| | Length Should Be            | ${messages}       | 3
| | Should Be Equal As Strings  | @{messages}[0]    | test message1
| | Should Be Equal As Strings  | @{messages}[1]    | test message2
| | Should Be Equal As Strings  | @{messages}[2]    | test message3
| | [Teardown]  | Unsubscribe and Disconnect  | ${topic}
